subprojects {
    def isRelease = isRelease(project)
    def releasedModules = isRelease ? releaseModules() : []

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    plugins.withType(MavenPublishPlugin) {
        publishing {
            publications {
                maven(MavenPublication) {

                    plugins.withType(JavaLibraryPlugin) {
                        from components.java
                        artifact sourcesJar
                        artifact javadocJar
                    }

                    pom {
                        groupId = project.group
                        name = project.name
                        afterEvaluate {
                            description = project.description
                        }
                        url = "https://jauntsdn.com"

                        licenses {
                            license {
                                name = "The Apache Software License, Version 2.0"
                                url = "http://www.apache.org/license/LICENSE-2.0.txt"
                                distribution = "repo"
                            }
                        }

                        developers {
                            developer {
                                id = "mostroverkhov"
                                name = "Maksym Ostroverkhov"
                                email = "m.ostroverkhov@gmail.com"
                            }
                        }

                        scm {
                            connection = "scm:git:https://github.com/jauntsdn/netty-websocket-http2.git"
                            developerConnection = "scm:git:https://github.com/jauntsdn/netty-websocket-http2.git"
                            url = "https://github.com/jauntsdn/netty-websocket-http2"
                        }

                        versionMapping {
                            usage("java-api") {
                                fromResolutionResult()
                            }
                            usage("java-runtime") {
                                fromResolutionResult()
                            }
                        }
                    }
                }
            }

            if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {

                task publishToSonatype {
                    finalizedBy "publishMavenPublicationToSonatypeRepository"
                    doLast {
                        println "\nPublishing ${project.name} artifacts to Sonatype"
                        println "Sonatype repository url: ${repositories.sonatype.url}\n"
                        publishing.publications.maven.publishableArtifacts
                                .collect { it.file.name }
                                .sort { it }
                                .each { println "[${project.name}] Uploading artifact..................${it}" }
                    }
                }

                repositories {
                    maven {
                        name = "sonatype"
                        def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                        def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                        url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
                        credentials {
                            username = project.property("ossrhUsername")
                            password = project.property("ossrhPassword")
                        }
                    }
                }
            }
        }

        plugins.withType(SigningPlugin) {
            signing {
                def isSigned = isRelease && releasedModules.contains(project.name)
                println("Publication signed: ${isSigned}")
                if (isSigned) {
                    sign publishing.publications.maven
                }
            }
        }
    }
}

def isRelease(Project project) {
    return project.hasProperty("release") && project.property("release") == "true"
}

def releaseModules() {
    return ["netty-websocket-http2", "netty-websocket-http2-callbacks-codec", "netty-websocket-multiprotocol"]
}